Import('env')


env = env.Clone(
    tools=['cython'],
    SHLIBPREFIX='',
    SHLIBSUFFIX='$PYTHON_EXT_SUFFIX',
)

env.Append(
    CPPPATH=['$PYTHONINCLUDES'],
    LIBPATH=['$PYTHONLIBPATH'],
    LIBS=['$PYTHONLIB'] + env.get(
        'SUNDIALS_LIBS',
        ['sundials_core', 'sundials_arkode', 'sundials_nvecserial'],
    ),
)

if env['PLATFORM'] == 'darwin':
    env.Append(
        RPATH=['@loader_path', '@executable_path'],
        LINKFLAGS=['-Wl,-rpath,@loader_path', '-Wl,-rpath,@executable_path'],
    )

env.VariantDir('.checkpoints', '.', duplicate=False)
shape_c = env.Cython('.checkpoints/shape.pyx')
shape = env.SharedLibrary('shape', shape_c)

Return('shape')
